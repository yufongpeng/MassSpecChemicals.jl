
"""
    abstract type AbstractElementalScheme <: AbstractScheme end

Scheme contaning exact elements.
"""
abstract type AbstractElementalScheme <: AbstractScheme end
"""
    abstract type AbstractStructuralScheme <: AbstractScheme end

Scheme contaning only structures.  
"""
abstract type AbstractStructuralScheme <: AbstractScheme end
"""
    abstract type AbstractCompleteScheme{T,S} <: AbstractScheme end

Scheme contaning both elements and structures. 
"""
abstract type AbstractCompleteScheme{T, S} <: AbstractScheme end
"""
    abstract type StructuralChemicalScheme <: AbstractStructuralScheme end

Scheme contaning structures generating chemical entity. 
"""
abstract type StructuralChemicalScheme <: AbstractStructuralScheme end

struct RandomProductScheme <: AbstractStructuralScheme end

"""
    abstract type StructuralElementalScheme{T,S} <: AbstractCompleteScheme{T,S} end

Default `AbstractCompleteScheme`

# Fields
* `structuralscheme::T`
* `elementalscheme::S`
"""
struct StructuralElementalScheme{T, S} <: AbstractCompleteScheme{T, S}
    structuralscheme::T 
    elementalscheme::S
end

"""

    ElementalScheme{Bool, T<:AbstractChemical} <: AbstractElementalScheme

Single scheme involving a chemical. The elements are fixed, and can be replaced by minor isotopes. 

* `ElementalScheme{false}`: chemical loss from a precursor. This product is not detected in MS; the other part of precursor is detected instead.
* `ElementalScheme{true}`: chemical gain to a precursor. This product is not detected in MS; the merged chemical is detected instead.

# Fields 
* `chemical::T`: chemical involved in scheme.

Single isotopomer can be set by using `Isotopomers` as field `chemical`. Elemental scheme can be redirected to the corresponding isotopic labeled scheme in `AdductIon` by dispatching on core chemical and existing schema, or looking up the `property` for generic `Chemical`.
"""
struct ElementalScheme{Bool, T<:AbstractChemical} <: AbstractElementalScheme
    chemical::T
    function ElementalScheme(gain::Bool, x::T) where {T <: AbstractChemical}
        new{gain, T}(x)
    end
end

"""
    ChemicalGain(chemical)

Chemical gain of `chemical`, i.e. `ElementalScheme(true, chemical)`.
"""
ChemicalGain(x) = ElementalScheme(true, x)

"""
    ChemicalLoss(chemical)

Chemical loss of `chemical`, i.e. `ElementalScheme(false, chemical)`.
"""
ChemicalLoss(x) = ElementalScheme(false, x)

"""
    ChemicalSchema{T<:AbstractScheme} <: AbstractScheme

Mutiple chemical schema.

# Fields
* `schema::Vector{Pair{T, Int}}`: scheme => number vector. The number v is the times of the scheme in the chemical schema. 
"""
struct ChemicalSchema{T<:AbstractScheme} <: AbstractScheme
    schema::Vector{T}
    number::Vector{Int}
end 

"""
    IsotopomerizedSchema{T<:ChemicalSchema} <: AbstractScheme

Mutiple chemical schema with delocalized isotopic replacements.

# Fields
* `schema::T`: parent scheme
* `isotopes::Vector{Pair{String, Int}}`: delocalized isotopic replacements
"""
struct IsotopomerizedSchema{T<:ChemicalSchema} <: AbstractScheme 
    parent::T
    isotopes::Vector{Pair{String, Int}}
end

function IsotopomerizedSchema(chemical::AbstractScheme, fullformula::String)
    IsotopomerizedSchema(chemicalparent(chemical), dictionary_elements(chemicalelements(fullformula)))
end

function IsotopomerizedSchema(chemical::AbstractScheme, fullelements::Dict)
    parent = chemicalparent(chemical)
    dp = dictionary_elements(chemicalelements(parent))
    dr = copy(fullelements)
    for k in keys(fullelements)
        iselement(k) && (delete!(dr, k); continue)
        dr[k] -= get(dp, k, 0) 
    end
    IsotopomerizedSchema(parent, collect(dr))
end


"""
    Groupedisotopomerizedschema{T<:AbstractScheme, N} <: AbstractScheme

Isotopomerized schema grouped by isotopomer state.

# Fields 
* `parent::T`: shared chemical scheme prior to isotopic replacement. 
* `state::Int`: isotopomer state.
* `isotope::String`: isotope for computing isotopomer state.
* `isotopes::Vector{Vector{Pair{String, Int}}}`: Isotopes-number pairs of isotopic replacements of each isotopomers.
* `abundance::Vector{N}`: abundance of each isotopomers.
"""
struct Groupedisotopomerizedschema{T<:AbstractScheme, N} <: AbstractScheme
    parent::T 
    state::Int
    isotope::String
    isotopes::Vector{Vector{Pair{String, Int}}}
    abundance::Vector{N}
    function Groupedisotopomerizedschema(parent::T, state::Int, isotope::String, isotopes::Vector{Vector{Pair{String, Int}}}, abundance::Vector{N}) where {T, N}
        id = sortperm(abundance)
        new{T, N}(parent, state, isotope, isotopes[id], abundance[id])
    end
end

schemetype(::ChemicalSchema{T}) where T = T 
schemetype(::T) where T = T 

function ChemicalSchema(scheme::T, schema...) where {T <: AbstractScheme} 
    C = promote_type(T, schemetype.(schema)...)
    cs = C[scheme]
    cn = Int[1]
    for s in schema
        push_scheme!(cs, cn, s)
    end
    ChemicalSchema(cs, cn)
end

function ChemicalSchema(schema::AbstractVector{T}) where {T <: AbstractScheme}
    cs = T[first(schema)]
    cn = Int[1]
    length(schema) < 2 && return ChemicalSchema(cs, cn)
    for s in @view schema[2:end]
        push_scheme!(cs, cn, s)
    end
    ChemicalSchema(cs, cn)
end

function ChemicalSchema(scheme::ChemicalSchema{T}, schema...) where {T <: AbstractScheme}
    C = promote_type(T, schemetype.(schema)...)
    if C == T
        cs = copy(scheme.schema)
    else
        cs = convert(Vector{C}, copy(scheme.schema))
    end
    cn = copy(scheme.number)
    for s in schema
        push_scheme!(cs, cn, s)
    end
    ChemicalSchema(cs, cn)
end

function push_scheme!(cs::Vector, cn::Vector, scheme::ChemicalSchema)
    for (k, v) in zip(scheme.schema, scheme.number)
        i = findfirst(==(k), cs)
        if i !== nothing
            cn[i] += v
        else
            push!(cs, k)
            push!(cn, v)
        end
    end
    cs
end

function push_scheme!(cs::Vector, cn::Vector, scheme::AbstractScheme)
    i = findfirst(==(scheme), cs)
    if i !== nothing
        cn[i] += 1
    else
        push!(cs, scheme)
        push!(cn, 1)
    end
    cs
end

"""
    const CompleteSchema = Union{<:AbstractCompleteScheme, <:ChemicalSchema{<:AbstractCompleteScheme}, <:IsotopomerizedSchema{<:ChemicalSchema{<:AbstractCompleteScheme}}, <:Groupedisotopomerizedschema{<:ChemicalSchema{<:AbstractCompleteScheme}}}

Complete scheme (scheme containing both `structuralscheme` and `elementalscheme`)
"""
const CompleteSchema = Union{<:AbstractCompleteScheme, <:ChemicalSchema{<:AbstractCompleteScheme}, <:IsotopomerizedSchema{<:ChemicalSchema{<:AbstractCompleteScheme}}, <:Groupedisotopomerizedschema{<:ChemicalSchema{<:AbstractCompleteScheme}}}

"""
    const StructuralSchema = Union{<:AbstractStructuralScheme, <:ChemicalSchema{<:AbstractStructuralScheme}, <:IsotopomerizedSchema{<:ChemicalSchema{<:AbstractStructuralScheme}}, <:Groupedisotopomerizedschema{<:ChemicalSchema{<:AbstractStructuralScheme}}}

Stuctural schema.
"""
const StructuralSchema = Union{<:AbstractStructuralScheme, <:ChemicalSchema{<:AbstractStructuralScheme}, <:IsotopomerizedSchema{<:ChemicalSchema{<:AbstractStructuralScheme}}, <:Groupedisotopomerizedschema{<:ChemicalSchema{<:AbstractStructuralScheme}}}

"""
    const ElementalSchema = Union{<:AbstractElementalScheme, <:ChemicalSchema{<:AbstractElementalScheme}, <:IsotopomerizedSchema{<:ChemicalSchema{<:AbstractElementalScheme}}, <:Groupedisotopomerizedschema{<:ChemicalSchema{<:AbstractElementalScheme}}}

Elemental schema.
"""
const ElementalSchema = Union{<:AbstractElementalScheme, <:ChemicalSchema{<:AbstractElementalScheme}, <:IsotopomerizedSchema{<:ChemicalSchema{<:AbstractElementalScheme}}, <:Groupedisotopomerizedschema{<:ChemicalSchema{<:AbstractElementalScheme}}}

"""
    const CompleteSchemeChemical = AbstractCompleteScheme{T,<:AbstractChemical}

Complete scheme 
"""
const CompleteSchemeChemical = AbstractCompleteScheme{T,<:AbstractChemical} where T